#!/usr/bin/perl
use strict;
use warnings;

use Data::Dumper;
use Getopt::Long qw(GetOptions);
use Pod::Usage   qw(pod2usage);

my %opt;
GetOptions(\%opt,
	'url=s',
	'username=s',
	'password=s',
) or pod2usage();

$opt{username} ||= 'admin';
pod2usage() if not $opt{url} or not $opt{username} or not $opt{password};

$opt{url} =~ s{/+$}{};

my %commands = (
	add_user   =>  [qw(uname email password)],
	list_users =>  [],
);

use Dwimmer::Client;


my $cmd = shift;

pod2usage() if not $cmd or $cmd eq 'help';

if ($commands{$cmd}) {
	my %data;
	my @fields = @{ $commands{$cmd} };

	# TODO check the validity of the parameters too
	if (@ARGV != @fields) {
		die "Invalid number of arguments. Needed: @{ $commands{$cmd} }\n";
	}
	@data{ @fields } = @ARGV;

	if ($cmd eq 'add_user') {
		$data{verify} = 'verified';
		$data{pw1} = $data{pw2} = delete $data{password};
	}

	my $dw = Dwimmer::Client->new( host => $opt{url} );
	my $login = $dw->login( username => $opt{username}, password => $opt{password} );
	if ($login->{error}) {
		if ($login->{error} eq 'invalid_password') {
			die "Invalid password\n";
		} elsif ($login->{error} eq 'not_logged_in') {
			die "Could not login\n";
			# maybe some error in the routing? e.g. multiple /-es somewhere?
		}
	}
	#print Dumper $login;
	my $r = $dw->$cmd(%data);

	if ($cmd eq 'list_users' ) {
		print "Users: \n\n";
		foreach my $u ( sort { $a->{name} cmp $b->{name} } @{ $r->{users} } ) {
			print "$u->{name}\n";
		}
	} elsif ($cmd eq 'add_user') {
		if ($r->{success}) {
			print "Success\n";
		} elsif ($r->{error}) {
			if ($r->{error} eq 'username_taken') {
				die "The username '$data{uname}' is already in use\n";
			} elsif ($r->{error} eq 'email_used') {
				die "The email '$data{email}' is already in use\n";
			} else {
				die 'Unrecognized error: ' . Dumper $r;
			}
		} else {
			die "No success and not error? " . Dumper $r;
		}
	} else {
		print Dumper $r;
	}
} else {
	pod2usage();
}
exit;

=head1 NAME

dwimmer - command line client to interact with a Dwimmer server

=head1 SYNOPSIS

OPTIONAL PARAMETER:

   --username  OF_ADMINISTRATOR    (admin by default)

REQUIRED PARAMETERS:

   --password  OF_ADMINISTRATOR
   --url       OF_DWIMMER_SITE


Commands:

   add_user USERNAME EMAIL PASSWORD
   list_users


For example if you would like to add a user called 'foo' with a password 'secret'
to the system you run

 dwimmer --url http://mycompany.com/ --password ADMIN_PASSWORD add_user foo foo@mycompany.com secret

If you'd like to list all the users run

 dwimmer --url http://mycompany.com/ --password ADMIN_PASSWORD list_users

=cut

